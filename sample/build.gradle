apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.2"
    defaultConfig {
        applicationId "com.cloudinary.android.sample"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // filter in the api credentials before building but without changing original source files - to make sure the
    // credentials are not checked into source control. The url (without api secret!) is taken from a gradle property
    // defined in gradle-local.properties in the project root.
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.processManifest.doLast {
                filterManifest("${buildDir}/intermediates/manifests/full/${variant.dirName}/AndroidManifest.xml", project.property("cloudinary.url"))
            }
        }
    }
}

dependencies {
    implementation project(':lib')
    implementation "androidx.lifecycle:lifecycle-extensions:2.0.0-rc01"
    implementation "androidx.appcompat:appcompat:1.0.0-rc02"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
    implementation "androidx.recyclerview:recyclerview:1.0.0-rc02"
    implementation "com.google.android.material:material:1.0.0-rc01"
    implementation "androidx.cardview:cardview:1.0.0-rc02"
    implementation ('com.squareup.picasso:picasso:2.71828'){
        exclude group: 'com.android.support'
    }

    implementation ("com.google.android.exoplayer:exoplayer:r2.4.4"){
        exclude group: 'com.android.support'
    }

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0-alpha4'

    testImplementation 'junit:junit:4.12'
}
