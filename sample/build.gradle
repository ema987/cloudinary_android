apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"
    defaultConfig {
        applicationId "com.cloudinary.android.sample"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // filter in the api credentials before building but without changing original source files - to make sure the
    // credentials are not checked into source control. The url (without api secret!) is taken from a gradle property
    // defined in gradle-local.properties in the project root.
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.processManifest.doLast {
                filterManifest("${buildDir}/intermediates/merged_manifests/${variant.dirName}/process${variant.dirName}Manifest/merged/AndroidManifest.xml", project.property("cloudinary.url"))
            }
        }
    }
}

dependencies {
    implementation project(':lib')
    implementation "com.android.support:appcompat-v7:${androidSupportVersion}"
    implementation "com.android.support.constraint:constraint-layout:1.1.3"
    implementation "com.android.support:recyclerview-v7:${androidSupportVersion}"
    implementation "com.android.support:design:${androidSupportVersion}"
    implementation "com.android.support:cardview-v7:${androidSupportVersion}"
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation "com.google.android.exoplayer:exoplayer:r2.4.4"

    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })

    testImplementation 'junit:junit:4.12'
}
